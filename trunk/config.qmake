########################################################################
# This is the main "dynanamic configuration" qmake code for the QBoard
# project. It is in a separate file for several reasons:
# 1) QDevelop has problems with all but the most basic qmake constructs.
# 2) Eventually the tree will be refactored into more modular subdirs,
#    and this will support that effort.
#
# You must edit the TOP_SRCDIR variable to the top-most directory of
# your QBoard source tree.
TOP_SRCDIR=/home/stephan/Projects/QBoard
!exists($$TOP_SRCDIR) {
    error("to fix this, edit config.qmake and set the variable TOP_SRCDIR to the \
	absolute path of the top-most directory of this source tree (the dir \
	containing .qmake.cache)")
}
########################################################################
# You should need to touch anything after that unless you're a QBoard
# maintainer.

########################################################################
# Construct a relative dir to the root. This won't work for TOP_SRCDIR,
# or at least it's untested.
REL_SRCDIR = $$(PWD)
REL_SRCDIR=$$replace(REL_SRCDIR,$$TOP_SRCDIR/,)
REL_SRCDIR=$$replace(REL_SRCDIR,[^/]+,..)

# qmake bug? feature? If you use CONFIG= instead of += the various
# Qt -Includes and such don't get added.
CONFIG += stl warn_on debug_and_release ordered 

QBOARD_VERSION = $$system(date +%Y%m%d)
# Reminder: i'd rather use %Y.%m.%d as QBOARD_VERSION, but apparently
# it's impossible to properly escape that to a string from here. i had
# it working once and qdevelop hosed the quoting when it re-saved
# the .pro file.
VERSION = $$QBOARD_VERSION

message(QBoard version $$QBOARD_VERSION)
message(QT_VERSION $$QT_VERSION)

RESOURCES_DIR = $$REL_SRCDIR/resources
UI_DIR = $$REL_SRCDIR/build
DESTDIR = $$REL_SRCDIR/bin
UI_SRCDIR = $$REL_SRCDIR/ui
SRC_DIR = $$REL_SRCDIR/src
S11N_DIR = $$REL_SRCDIR/s11n
S11N_INCLUDES = -I$$S11N_DIR/include
S11N_CXXFLAGS = $$S11N_INCLUDES
QBOARD_CXXFLAGS = $$S11N_CXXFLAGS -DQBOARD_VERSION=$$QBOARD_VERSION -I$$REL_SRCDIR/src
MOC_DIR = $$REL_SRCDIR/build
OBJECTS_DIR = $$REL_SRCDIR/build
S11N_OBJ_DIR = $$OBJECTS_DIR/s11n

QBOARD_INCLUDES = -I$$REL_SRCDIR/src -I$$MOC_DIR -I$$UI_DIR
S11N_DIR = $$REL_SRCDIR/s11n
S11N_SOURCES = $$S11N_DIR/data_node_io.cpp \
 $$S11N_DIR/exception.cpp \
 $$S11N_DIR/funtxt.flex.cpp \
 $$S11N_DIR/funtxt_serializer.cpp \
 $$S11N_DIR/funxml.flex.cpp \
 $$S11N_DIR/funxml_serializer.cpp \
 $$S11N_DIR/parens.flex.cpp \
 $$S11N_DIR/parens_serializer.cpp \
 $$S11N_DIR/path_finder.cpp \
 $$S11N_DIR/plugin.cpp \
 $$S11N_DIR/s11n.cpp \
 $$S11N_DIR/s11n_node.cpp \
 $$S11N_DIR/s11nlite.cpp \
 $$S11N_DIR/strtool.cpp
S11N_DIR=

contains(QT_VERSION, ^4\.4\..+ ){
	 message("Adding Qt 4.4-specific bits")
	 QBOARD_HEADERS_QT44 = $$SRC_DIR/QBoardDocsBrowser.h 
	 QBOARD_SOURCES_QT44 = $$SRC_DIR/QBoardDocsBrowser.cpp
	 QBOARD_FORMS_QT44 = $$UI_SRCDIR/QBoardDocsBrowser.ui
	 QBOARD_QT44 = webkit
}
QT = core gui opengl $$QBOARD_QT44

unix:DOT_OBJ=.o
win32:DOT_OBJ=.obj

S11N_OBJECT =
for(CPP, S11N_SOURCES){
  BASENAME = $$basename(CPP)
  S11N_OBJECTS += $$S11N_OBJ_DIR/$$replace(BASENAME,.cpp,$$DOT_OBJ)
}
# error(S11N_OBJECTS=$$S11N_OBJECTS)


QBOARD_HEADERS_LIB = \
 $$SRC_DIR/GamePiece.h \
 $$SRC_DIR/GameState.h \
 $$SRC_DIR/MenuHandlerGeneric.h \
 $$SRC_DIR/MenuHandlerPiece.h \
 $$SRC_DIR/QBoard.h \
 $$SRC_DIR/QBoardView.h \
 $$SRC_DIR/QGI.h \
 $$SRC_DIR/QGIGamePiece.h \
 $$SRC_DIR/QGIHtml.h \
 $$SRC_DIR/QGILine.h \
 $$SRC_DIR/S11n.h \
 $$SRC_DIR/S11nClipboard.h \
 $$SRC_DIR/S11nQt.h \
 $$SRC_DIR/S11nQtList.h \
 $$SRC_DIR/S11nQtListReg.h \
 $$SRC_DIR/Serializable.h \
 $$SRC_DIR/SetupQBoard.h \
 $$SRC_DIR/reg_qt_s11n.h \
 $$SRC_DIR/utility.h


QBOARD_SOURCES_LIB = \
 $$SRC_DIR/GamePiece.cpp \
 $$SRC_DIR/GameState.cpp \
 $$SRC_DIR/MenuHandlerGeneric.cpp \
 $$SRC_DIR/MenuHandlerPiece.cpp \
 $$SRC_DIR/QBoard.cpp \
 $$SRC_DIR/QBoardView.cpp \
 $$SRC_DIR/QGIGamePiece.cpp \
 $$SRC_DIR/QGIHtml.cpp \
 $$SRC_DIR/QGILine.cpp \
 $$SRC_DIR/S11nClipboard.cpp \
 $$SRC_DIR/S11nQt.cpp \
 $$SRC_DIR/Serializable.cpp \
 $$SRC_DIR/SetupQBoard.cpp \
 $$SRC_DIR/utility.cpp

QBOARD_SOURCES_WINDOW = \
 $$SRC_DIR/AboutQBoardImpl.cpp \
 $$SRC_DIR/FileBrowser.cpp \
 $$SRC_DIR/MainWindowImpl.cpp \
 $$QBOARD_SOURCES_QT44

QBOARD_HEADERS_WINDOW = \
 $$SRC_DIR/AboutQBoardImpl.h \
 $$SRC_DIR/FileBrowser.h \
 $$SRC_DIR/MainWindowImpl.h \
 $$QBOARD_HEADERS_QT44

QBOARD_SOURCES_MAINAPP = \
 $$SRC_DIR/main.cpp
